plugins {
	id 'net.researchgate.release' version '2.4.0'
}

defaultTasks 'release'

checkCommitNeeded.doFirst {

	println "> upgrading docker-compose version=${project.version}"

	updateVersion("${project.projectDir}/docker-compose.yml", "(dns-proxy-server.*)(\\d+\\.\\d\\.+\\d+)", "\$1${project.version}")
	updateVersion("${project.projectDir}/Dockerfile.hub", '(\\d+\\.\\d\\.+\\d+)', "${project.version}")
	updateVersion("${project.projectDir}/VERSION", '(\\d+\\.\\d\\.+\\d+)', "${project.version}")

	def sout = new StringBuilder()
	def proc = ['git', 'commit', '-a', "-m [Gradle Release Plugin] - releasing '${project.version}'."].execute()
	proc.consumeProcessOutput(sout, sout)
	proc.waitForOrKill(3000)
	println "> $sout"

}
release {
	project.ext.set("release.useAutomaticVersion", true)
	failOnCommitNeeded = false
	failOnPublishNeeded = false
	failOnUnversionedFiles = false
	buildTasks = []
	git {
		requireBranch = 'master'
		pushToRemote = ''
	}
}

void updateVersion(f, pattern, replace){

	f = file(f)
	println "> updating version in: ${f}"
	if(!f.exists()){
		println "file ${f} not found"
		System.exit(-1)
	}
	f.write(f.text.replaceAll(pattern, replace))

}
